presentation Scenario2_IntroPython

theme font "Arial, Helvetica, sans-serif" primary "#0055A4"

header bg "#0055A4" color "#FFFFFF" size "1.6rem" {
    box {
        text {Introduction to Programming in Python}
    }
}

footer bg "#F5F5F5" color "#333333" size "1rem" {
    box {
        text {© 2026 - Python basics}
    }
}

slide title {
    box [alignment = "center"] {
        text [bold, text-size = "xl"] {Introduction to Python}
        text {Variables • Conditions • Loops • Functions}
    }
}

slide outline {
    box [alignment = "top left"] {
        text [bold, underline] {Outline}
        list [type = ordered, spaceBetweenItems = 18] {
            {What is a program?}
            {Python syntax in 2 minutes}
            {Line-by-line code walkthrough}
            {Mixing text and pictures}
        }
    }
}

slide objectives {
    box {
        text [bold] {Learning objectives}
        list [type = unordered, spaceBetweenItems = 14] {
            {Read basic Python code}
            {Understand variables and control flow}
            {Write a small function}
        }
    }
}

slide code_walkthrough_1 {
    box {
        text [bold] {Example: reading a sensor value}
        code {
            "python"
            ```
value = 42

if value >= 50:
    print("ALERT")
else:
    print("OK")
            ```

            line {1 image { src {../src/example/assets/logo.jpg} alt {Step 1: variable} } }
            line {3..4 image { src {../src/example/assets/sites.png} alt {Step 2: condition + alert path} } }
            line {5..6 image { src {../src/example/assets/sites.png} alt {Step 3: else path} } }
        }
    }
}

slide code_walkthrough_2 {
    box {
        text [bold] {Example: a simple function}
        code {
            "python"
            ```
def area(width, height):
    return width * height

print(area(3, 4))
            ```

            line {1 image { src {../src/example/assets/logo.jpg} alt {Function definition} } }
            line {2 image { src {../src/example/assets/sites.png} alt {Return statement} } }
            line {4 image { src {../src/example/assets/sites.png} alt {Call the function} } }
        }
    }
}

slide mix_text_image_left {
    box [column = 2, height = 100%] {
        box {
            text [bold] {Key idea}
            text {Python uses indentation to define code blocks.}
            text {Keep it consistent (4 spaces is typical).}
        }
        image {
            src {../src/example/assets/sites.png}
            alt {Illustration}
        }
    }
}

slide mix_text_image_positions {
    box [height = 100%, width = 100%] {
        text [alignment = "top left", bold] {Top-left: explanation}
        text [alignment = "bottom right"] {Bottom-right: caption}
        image [alignment = "center"] {
            src {../src/example/assets/logo.jpg}
            alt {Centered picture}
        }
    }
}
